apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

apply from: "$rootProject.projectDir/gradle/version.gradle"

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = versionName

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version versionName

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                // Metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'AdvancedRadioGroup library for Android'
                    url = 'https://github.com/ihermandev/advanced-radio-group'
                    licenses {
                        license {
                            name = 'Apache 2.0 license'
                            url = 'https://github.com/ihermandev/advanced-radio-group/blob/master/LICENSE.md'
                        }
                    }
                    developers {
                        developer {
                            id = 'ihermandev'
                            name = 'Illia Herman'
                            email = 'illiaherman8@gmail.com'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:git:github.com/ihermandev/advanced-radio-group.git'
                        developerConnection = 'scm:git:ssh://github.com/ihermandev/advanced-radio-group.git'
                        url = 'https://github.com/ihermandev/advanced-radio-group/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}